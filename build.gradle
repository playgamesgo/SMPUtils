plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/smputils.accesswidener")
}


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url = uri('https://mcef-download.cinemamod.com/repositories/releases')
    }
    maven {
        url 'https://maven.wispforest.io/releases/'
    }
    maven {
        name = "FzzyMaven"
        url = uri("https://maven.fzzyhmstrs.me/")
    }
    maven {
        name = "JackFredMaven"
        url = uri("https://maven.jackf.red/releases/")
    }
    maven {
        name = "Xander Snapshot Maven"
        url = uri("https://maven.isxander.dev/snapshots/")
    }
    maven {
        url = "https://maven.quiltmc.org/repository/release/"
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    repositories {
        maven {
            name = "Terraformers"
            url = "https://maven.terraformersmc.com/"
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "io.wispforest:owo-lib:${project.owo_version}"
    include "io.wispforest:owo-sentinel:${project.owo_version}"

//    modCompileOnly 'com.github.CCBlueX:mcef:1.3.2-1.21.4'
//    modRuntimeOnly 'com.github.CCBlueX:mcef:1.3.2-1.21.4'
    modImplementation 'com.github.CCBlueX:mcef:1.3.2-1.21.4'

    compileOnly "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.projectlombok:lombok:1.18.32"

    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
    modImplementation "maven.modrinth:litematica:${project.litematica_version}"
    modImplementation "maven.modrinth:malilib:${project.malilib_version}"
    modImplementation("red.jackf:whereisit:${project.whereIsItVersion}")
    include("red.jackf:whereisit:${project.whereIsItVersion}")
    modImplementation("dev.isxander:yet-another-config-lib:${project.yacl_version}") {
        exclude group: "com.terraformersmc", module: "modmenu"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "yacl_version", project.yacl_version
    inputs.property "mcef_version", project.mcef_version
    inputs.property "litematica_version", project.litematica_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "yacl_version": project.yacl_version,
                "mcef_version": project.mcef_version,
                "litematica_version": project.litematica_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
